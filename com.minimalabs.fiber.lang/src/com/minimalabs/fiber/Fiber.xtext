grammar com.minimalabs.fiber.Fiber with org.eclipse.xtext.common.Terminals

generate fiber "http://www.minimalabs.com/fiber/Fiber"

Model:
  (types+=Type)*
  (services+=Service)*;
  
Type:
  ClassType|Enumeration;

ClassType:
  SimpleType|Class;

SimpleType:
  "type" name=ID ("is" mappedType=TypeId)?;

TypeId:
  name=ID ("." ID)*;

Class:
  "class" name=ID (":" superTypes+=[ClassType] ("," superTypes+=[ClassType])*)? "{" (attributes+=Attribute)* "}";

Attribute:
  type=[Type] (manyReference=AttributeManyReference)? name=ID;

enum AttributeManyReference: 
  NONE | WEAK = "*" | STRONG = "#";

Enumeration:
  "enum" name=ID "{" (literals+=EnumerationLiteral)* "}";

EnumerationLiteral:
  name=ID "(" ordinal=INT ")";
  
Service:
  "service" name=ID "{" (methods+=Method)* "}";

Method:
  returnType=[Type] name=ID "(" (parameters+=Parameter)? ("," parameters+=Parameter)* ")";

Parameter:
  type=[Type] name=ID;


  	
